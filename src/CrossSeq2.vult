
fun soft(x1) {
   mem x2,x3,x4;
   val o = (x1+x2+x3+x4)/4.0;
   x2,x3,x4 = x1,x2,x3;
   return o;
}

fun change(x:real):bool {
    mem pre_x;
    val v:bool = pre_x <> x;
    pre_x = x;
    return v;
}

fun edge(x:bool) : bool {
   mem pre;
   val ret = x && not(pre);
   pre = x;
   return ret;
}

// Returns true every 'n' calls
fun each(n){
   mem count;
   val ret = (count == 0);
   count = (count + 1) % n;
   return ret;
}

fun lerp(a,b,i){

    return a * (1.0 - i) + (b *  i);
}

fun lfo_interp(cv, shape, amt, pw, reset, sampleTime) {
   mem rate; 
   mem phase;
   mem o;
   if(change(cv))
      rate = cv * sampleTime;
   val update = each(8); 
   if(update)
      phase = phase + rate;
   phase = if phase > 2.0 then phase - 2.0 else phase;
   val breset = reset > 0.0;
   if(edge(breset))
      phase = 0.0;
   val tmp = phase - 1.0;
   if(update)
      o =
         if shape < 1.0 then 
            lerp((sin(phase*3.141592653589793)),(abs(tmp) * 2.0 - 1.0),shape % 1.0)*amt
         else if shape < 2.0 then
            lerp((abs(tmp) * 2.0 - 1.0), tmp, shape % 1.0)*amt
         else if shape < 3.0 then 
            lerp(tmp, (if tmp > pw then 1.0 else -1.0), shape % 1.0)*amt
         else
            (if tmp > pw then 1.0 else -1.0)*amt;
   return soft(o);
}

fun cross_detect(a:real, b:real)
{
    val diff = a - b;
    val sign = if diff > 0.0 then 1.0 else 0.0;
    val trig = if change(sign) then 1.0 else 0.0;
    return trig;
}

fun process(sampleTime:real){
   mem sync, freq, rate1, rate2, amt1, amt2, shape1, shape2, pw1, pw2;
   val lfo1 = lfo_interp(rate1*freq, shape1, amt1, pw1, sync,sampleTime) ;
   val lfo2 = lfo_interp(rate2*freq, shape2, amt2, pw2, sync,sampleTime) ;
   val gate = cross_detect(lfo1, lfo2);
   //val env = ad(gate, 0.0, 0.3);
   val trig = gate;
   val diff = (lfo1 - lfo2)  / (amt1 + amt2);
   return trig, lfo1 / amt1, lfo2 / amt2, diff ; 
}
and setFreq(knob:real, input:real){
   freq = knob + input;
}
and setSync(value:real){
   sync = value;
}
and setRate1(knob:real, input:real, phaseKnob:real){
   rate1 = knob + input + phaseKnob;
}
and setRate2(knob:real, input:real, phaseKnob:real){
   rate2 = knob + input + phaseKnob;
}
and setAmt1(knob:real, input:real){
   amt1 = knob + input;
}
and setAmt2(knob:real, input:real){
   amt2 = knob + input;
}
and setShape1(knob:real, input:real){
   shape1 = knob + input;
}
and setShape2(knob:real, input:real){
   shape2 = knob + input;
}
and setPw1(knob:real){
   pw1 = knob * 2. - 1.;
}
and setPw2(knob:real){
   pw2 = knob * 2. - 1.;
}


// Called on initialization to define initial values
and default() @[init]{
   mem freq = 1.0;
   mem sync = 0.0;
   mem rate1 = 1.0;
   mem rate2 = 1.0;
   mem amt1 = 1.0;
   mem amt2 = 1.0;
   mem shape1 = 0.0;
   mem shape2 = 1.0;
   mem phase1 = 0.0;
   mem phase2 = 0.0;
   mem pw1 = 0.5;
   mem pw2 = 0.5;
}