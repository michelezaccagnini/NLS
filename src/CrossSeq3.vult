
fun soft(x1) {
   mem x2,x3,x4;
   val o = (x1+x2+x3+x4)/4.0;
   x2,x3,x4 = x1,x2,x3;
   return o;
}

fun edge(x:bool) : bool {
   mem pre;
   val ret = x && not(pre);
   pre = x;
   return ret;
}

fun change(x:real):bool {
    mem pre_x;
    val v:bool = pre_x <> x;
    pre_x = x;
    return v;
}

// Returns true every 'n' calls
fun each(n){
   mem count;
   val ret = (count == 0);
   count = (count + 1) % n;
   return ret;
}


fun lerp(a,b,i){

    return a * (1.0 - i) + (b *  i);
}

fun lfo_interp(cv, shape, amt, pw, reset, phaseOff, sampleTime) {
   mem rate; 
   mem phase;
   mem o;
   if(change(cv))
      rate = cv * sampleTime * 8.0;
   val update = each(8); 
   if(update)
      phase = phase + rate;
   phase = if (phase + phaseOff) > 2.0 then (phase + phaseOff) - 2.0 else phase + phaseOff;
   //for smooth interpolation between sine and tri I need to offset the phase of the
   //tri
   val offset_phase = if phase + 0.5 > 2.0 then (phase + 0.5) - 2.0 else phase + 0.5;
   val breset = reset > 0.0;
   if(edge(breset))
      phase = 0.0;
   val saw = phase - 1.0;
   offset_phase = offset_phase - 1.0;
   //(abs(offset_phase - 1.5) * 2.0 - 1.0)*-1.0
   if(update)
      o =
         if shape < 1.0 then 
            lerp((sin(phase*3.141592653589793)), (abs(offset_phase) * 2.0 - 1.0)*-1.0, shape % 1.0)*amt
         else if shape < 2.0 then
            lerp((abs(offset_phase) * 2.0 - 1.0)*-1.0, saw, shape % 1.0)*amt
         else if shape < 3.0 then 
            lerp(saw, (if saw > pw then 1.0 else -1.0), shape % 1.0)*amt
         else
            (if saw > pw then 1.0 else -1.0)*amt;
   return soft(o);
}

fun cross_detect(a:real, b:real, st:real)
{
   mem open:real;
   open = open + st;
   val diff = a - b;
   val sign = if diff > 0.0 then 1.0 else 0.0;
   if (change(sign))
      open = 0.0;
   return if open > 0.001 then 0.0 else 1.0;
}

fun cross_detect3(a:real, b:real, c:real)
{
    val diff12 = a - b;
    val diff13 = a - c;
    val diff23 = b - c;
    val adiff = abs(diff12) + abs(diff13) + abs(diff23); 
    val ret = if adiff < 0.1 then 1.0 else 0.0; 
    return ret;
}

fun process(sampleTime:real){
   mem sync, freq, rate1, rate2, rate3, amt1, amt2, amt3, shape1, shape2, shape3, pw1, pw2, pw3,phaseOff1, phaseOff2, phaseOff3;
   val lfo1 = lfo_interp(rate1*freq, shape1, amt1, pw1, sync, phaseOff1, sampleTime) ;
   val lfo2 = lfo_interp(rate2*freq, shape2, amt2, pw2, sync, phaseOff2, sampleTime) ;
   val lfo3 = lfo_interp(rate3*freq, shape3, amt3, pw3, sync, phaseOff3, sampleTime) ;
   val tr12 = cross_detect(lfo1, lfo2,sampleTime);
   val tr13 = cross_detect(lfo1, lfo3,sampleTime);
   val tr23 = cross_detect(lfo2, lfo3,sampleTime);
   val tr123 = cross_detect3(lfo1, lfo2, lfo3);
   val diff12 = (lfo1 - lfo2) / (amt1 + amt2 + 0.01 );
   val diff13 = (lfo1 - lfo3) / (amt1 + amt3 + 0.01 );
   val diff23 = (lfo2 - lfo3) / (amt2 + amt3 + 0.01 );
   val diff123 = (lfo1 - lfo2 - lfo3) / (amt1 + amt2 + amt3 + 0.01 ) ;
   lfo1 = lfo1 / amt1;
   lfo2 = lfo2 / amt2;
   lfo3 = lfo3 / amt3;
   return tr12, tr13, tr23, tr123, lfo1, lfo2, lfo3, diff12, diff13, diff23, diff123 ; 
}
and setFreq(knob, input:real){
   freq = knob + input;
}
and setSync(value){
   sync = value;
}
and setRate1(knob, input:real){
   rate1 = knob + input;
}
and setRate2(knob, input:real){
   rate2 = knob + input ;
}
and setRate3(knob, input:real){
   rate3 = knob + input ;
}
and setPhase1(phaseKnob:real){
   phaseOff1 = phaseKnob;
}
and setPhase2(phaseKnob:real){
   phaseOff2 = phaseKnob;
}
and setPhase3(phaseKnob:real){
   phaseOff3 = phaseKnob;
}
and setAmt1(knob, input:real){
   amt1 = knob + input;
}
and setAmt2(knob, input:real){
   amt2 = knob + input;
}
and setAmt3(knob, input:real){
   amt3 = knob + input;
}
and setShape1(knob, input:real){
   shape1 = knob + input;
}
and setShape2(knob, input:real){
   shape2 = knob + input;
}
and setShape3(knob, input:real){
   shape3 = knob + input;
}
and setPw1(knob:real){
   pw1 = knob * 2.0 - 1.0;
}
and setPw2(knob:real){
   pw2 = knob * 2.0 - 1.0;
}
and setPw3(knob:real){
   pw3 = knob * 2.0 - 1.0;
}


// Called on initialization to define initial values
and default(){
   mem freqK = 1.0;
   mem sync = 0.0;
   mem rate1 = 1.0;
   mem rate2 = 1.0;
   mem rate3 = 1.0;
   mem amt1 = 1.0;
   mem amt2 = 1.0;
   mem amt3 = 1.0;
   mem shape1 = 0.0;
   mem shape2 = 1.0;
   mem shape3 = 1.0;
   mem phase1 = 0.0;
   mem phase2 = 0.0;
   mem phase3 = 0.0;
   mem pw1 = 0.5;
   mem pw2 = 0.5;
   mem pw3 = 0.5;
   mem x2 = 0.0;
   mem x3 = 0.0;
   mem x4 = 0.0;
   mem pre_x = 0.0;
   mem pre = 0.0;
   mem count = 0.0;
   mem phase = 0.0;
   mem rate = 1.0;
}